``` abap
*&---------------------------------------------------------------------*
*& Form handle_double_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW
*&      --> E_COLUMN
*&---------------------------------------------------------------------*
FORM handle_double_click  USING    pv_row
                                   pv_column
                                   po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.

*-- 정비 오더 헤더 ALV에 한해 이벤트 발생
    WHEN go_disp_ooh_alv.

*-- 클릭한 행의 오더 ID와 스케줄 ID 정보
      DATA(lv_order) = gt_disp_ooh[ pv_row ]-order_id.
      DATA(lv_sch) = gt_disp_ooh[ pv_row ]-scheduleid.

*-- 정비 오더 아이템 정보 초기화
      CLEAR gt_disp_ooi.

*-- 스케줄 ID를 바탕으로 스케줄 정보 조회
      SELECT SINGLE
        FROM zc103sdt0008
        FIELDS scheduleid, flightid, countryfrom, countryto, departdate, departtime, arrivedate, arrivetime, flighttime
        WHERE scheduleid = @lv_sch
        INTO CORRESPONDING FIELDS OF @gs_sch.

*-- Secondaty Index Search로 도메인 값 검색( = Binary Search )
      gv_str = VALUE #( gt_dd07v[ KEY key domvalue_l = gs_sch-countryfrom ]-ddtext ).

      gs_sch-countryfrom = gv_str.

      CLEAR gv_str.

      gv_str = VALUE #( gt_dd07v[ KEY key domvalue_l = gs_sch-countryto ]-ddtext ).

      gs_sch-countryto = gv_str.

*-- ALV에서 일어난 이벤트이기에 PBO로 안감. 그래서 강제로 스크린 이벤트 발생
      CALL METHOD cl_gui_cfw=>set_new_ok_code
        EXPORTING
          new_code = 'ENTER'.

*-- 정비 오더 아이템 원본 데이터로부터 ALV용 테이블로 데이터 삽입
      LOOP AT gt_ooi ASSIGNING FIELD-SYMBOL(<fs>) WHERE order_id = lv_order.

        CLEAR : gs_disp_ooi.

        gs_disp_ooi = CORRESPONDING #( <fs> ).

        APPEND gs_disp_ooi TO gt_disp_ooi.

      ENDLOOP.

*-- 화면 초기화
      PERFORM set_screen.
      PERFORM refresh_table USING go_disp_ooi_alv.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_hotspot_click
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_ROW_ID
*&      --> E_COLUMN_ID
*&---------------------------------------------------------------------*
FORM handle_hotspot_click  USING    pv_row_id
                                    pv_column_id
                                   po_sender TYPE REF TO cl_gui_alv_grid.

  CASE po_sender.
    WHEN go_disp_ooh_alv.

*-- 핫스팟 행의 데이터 읽어온 뒤 그에 해당하는 항공기 데이터 SALV로 출력
      READ TABLE gt_disp_ooh ASSIGNING FIELD-SYMBOL(<fs>) INDEX pv_row_id.
      PERFORM salv_display USING <fs>-aircraft_id.
  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_edit_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_OBJECT
*&      --> E_INTERACTIVE
*&---------------------------------------------------------------------*
FORM handle_edit_toolbar USING po_object TYPE
                                         REF TO cl_alv_event_toolbar_set
                               pv_interactive
                               po_sender TYPE REF TO cl_gui_alv_grid.
  DATA : lv_disabled .

  CASE po_sender.
    WHEN go_disp_ooh_alv.

*-- 조회 모드에 따른 동작 변경
      CASE gv_modeh.
        WHEN 'E'.
          lv_disabled = abap_false.
        WHEN 'R'.
          lv_disabled = abap_true.
      ENDCASE.

      PERFORM make_divide_line USING po_object.

*-- 정비 오더 헤더 생성 버튼
*      PERFORM make_btn USING 'CREAH' icon_create
*                             'Create Order Header' ' ' po_object.

*-- 정비 오더 헤더 입력 모드 전환 버튼
      PERFORM make_btn USING 'DISPH' icon_toggle_display_change
                             '입력 모드 전환' ' ' po_object.

      PERFORM make_divide_line USING po_object.

*-- 정비 오더 헤더 삭제 버튼
      PERFORM make_btn USING 'DROWH' icon_delete_row
                             'Delete row' lv_disabled po_object.

    WHEN go_disp_ooi_alv.

*-- 조회 모드에 따른 동작 변경
      CASE gv_modei.
        WHEN 'E'.
          lv_disabled = abap_false.
        WHEN 'R'.
          lv_disabled = abap_true.
      ENDCASE.


      PERFORM make_divide_line USING po_object.

*-- 정비 오더 아이템 생성 버튼
*      PERFORM make_btn USING 'CREAI' icon_create
*                             'Create Order Header' ' ' po_object.

*-- 정비 오더 아이템 입력 모드 전환 버튼
      PERFORM make_btn USING 'DISPI' icon_toggle_display_change
                             '입력 모드 전환' ' ' po_object.

      PERFORM make_divide_line USING po_object.

*-- 정비 오더 아이템 삭제 버튼
      PERFORM make_btn USING 'DROWI' icon_delete_row
                             'Delete row' lv_disabled po_object.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form make_divide_line
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> PO_OBJCET
*&---------------------------------------------------------------------*
FORM make_divide_line USING po_object TYPE
                                      REF TO cl_alv_event_toolbar_set.

  po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( butn_type = '3' ) ).

ENDFORM.

*&---------------------------------------------------------------------*
*& Form make_btn
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      --> ICON_TOGGLE_DISPLAY_CHANGE
*&      --> P_
*&---------------------------------------------------------------------*
FORM make_btn USING pv_function pv_icon pv_quick pv_disabled
                    po_object TYPE REF TO cl_alv_event_toolbar_set.

  po_object->mt_toolbar = VALUE #( BASE po_object->mt_toolbar ( function  = pv_function
                                                                disabled  = pv_disabled
                                                                icon      = pv_icon
                                                                quickinfo = pv_quick ) ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_modify_value
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_MODIFIED
*&      --> ET_GOOD_CELLS
*&---------------------------------------------------------------------*
FORM handle_modify_value USING pv_modified
                               pt_good_cells TYPE lvc_t_modi
                               po_sender TYPE REF TO cl_gui_alv_grid.
*-- Check Modified
  CHECK pv_modified IS NOT INITIAL.

*-- 변경 플래그 설정
  CASE po_sender.

    WHEN go_disp_ooh_alv.

      LOOP AT pt_good_cells ASSIGNING FIELD-SYMBOL(<fs>).

        READ TABLE gt_disp_ooh ASSIGNING FIELD-SYMBOL(<fs2>) INDEX <fs>-row_id.

        <fs2>-modi_yn = abap_true.

      ENDLOOP.

    WHEN go_disp_ooi_alv.

      LOOP AT pt_good_cells ASSIGNING FIELD-SYMBOL(<fs3>).

        READ TABLE gt_disp_ooi ASSIGNING FIELD-SYMBOL(<fs4>) INDEX <fs3>-row_id.

        <fs4>-modi_yn = abap_true.

      ENDLOOP.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form handle_user_command
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> E_UCOMM
*&---------------------------------------------------------------------*
FORM handle_user_command USING pv_ucomm
                               po_sender TYPE REF TO cl_gui_alv_grid.

  CASE pv_ucomm.

    WHEN 'CREAH'.
*-- 정비 오더 헤더 생성
      CALL SCREEN 200 STARTING AT 70 10 .

*-- 정비 오더 헤더 입력 모드 전환
    WHEN 'DISPH'.
      PERFORM toggle_changeh.

    WHEN 'DROWH'.
*-- 정비 오더 헤더 삭제
      PERFORM delete_rowh.

    WHEN 'CREAI'.
*-- 정비 오더 아이템 생성
      CALL SCREEN 300 STARTING AT 180 10 .

*-- 정비 오더 아이템 입력 모드 전환
    WHEN 'DISPI'.
      PERFORM toggle_changei.

    WHEN 'DROWI'.
*-- 정비 오더 아이템 삭제
      PERFORM delete_rowi.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form on_date_selected
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> DATE_BEGIN
*&      --> SENDER
*&---------------------------------------------------------------------*
FORM on_date_selected  USING    pv_date
                                po_sender TYPE REF TO cl_gui_calendar.

*-- 초기 켈린더에 찍힐 날짜
  DATA : r3_today TYPE cnca_utc_date.

  CASE po_sender.

    WHEN go_dep_cal.

*-- From 날짜가 To 날짜보다 작거나 같다면 정상 작동
      IF pv_date <= gv_arr_date.

        gv_dep_date = pv_date.

*-- From 날짜가 To 날짜 이후일 때 동작
      ELSEIF pv_date > gv_arr_date.

*-- 메시지 처리
        MESSAGE '잘못된 날짜 입력' TYPE 'S' DISPLAY LIKE 'E'.

*-- From 날짜를 To 날짜로 변경
        gv_dep_date = gv_arr_date.

*-- 켈린더에 Refresh가 없으므로 삭제 후 다시 생성
        go_dep_cal->free( ).

        FREE : go_dep_cal.

*-- 변경할 날짜 입력
        r3_today = gv_arr_date.

*-- 변경된 From 날짜를 기준으로 From 켈린더 다시 생성
        CREATE OBJECT go_dep_cal
          EXPORTING
            parent          = go_dep_con
            view_style      = gv_calendar_style
            selection_style = gv_selection_style
            focus_date      = r3_today.

*-- 이벤트도 다시 설치
        SET HANDLER lcl_event_handler=>on_date_selected  FOR go_dep_cal.

*-- 켈린더에 여러 이벤트들 다시 설치
        myevent-appl_event = ' '.
        myevent-eventid = go_dep_cal->m_id_date_selected.
        APPEND myevent TO myevent_tab.

        myevent-appl_event = 'X'.
        myevent-eventid = go_dep_cal->m_id_pre_selection.
        APPEND myevent TO myevent_tab.

        myevent-eventid = go_dep_cal->m_id_info_request.
        APPEND myevent TO myevent_tab.

        myevent-eventid = go_dep_cal->m_id_ctxmenu_request.
        APPEND myevent TO myevent_tab.

        myevent-eventid = go_dep_cal->m_id_ctxmenu_selected.
        APPEND myevent TO myevent_tab.

        CALL METHOD go_dep_cal->set_registered_events
          EXPORTING
            events = myevent_tab.
      ENDIF.

    WHEN go_arr_cal.

*-- To 날짜가 From 날짜보다 크거나 같다면 정상 작동
      IF  pv_date >= gv_dep_date.

        gv_arr_date = pv_date.

*-- To 날짜가 From 날짜 이전일 때 동작
      ELSEIF pv_date < gv_dep_date.

        MESSAGE '잘못된 날짜 입력' TYPE 'S' DISPLAY LIKE 'E'.

*-- To 날짜를 From 날짜로 변경
        gv_arr_date = gv_dep_date.

*-- 켈린더에 Refresh가 없으므로 삭제 후 다시 생성
        go_arr_cal->free( ).

        FREE : go_arr_cal.

*-- 변경할 날짜 입력
        r3_today = gv_arr_date.

*-- 변경된 To 날짜를 기준으로 To 켈린더 다시 생성
        CREATE OBJECT go_arr_cal
          EXPORTING
            parent          = go_arr_con
            view_style      = gv_calendar_style
            selection_style = gv_selection_style
            focus_date      = r3_today.

*-- 이벤트도 다시 설치
        SET HANDLER lcl_event_handler=>on_date_selected  FOR go_arr_cal.


*-- 켈린더에 여러 이벤트들 다시 설치
        myevent-appl_event = ' '.
        myevent-eventid = go_arr_cal->m_id_date_selected.
        APPEND myevent TO myevent_tab.

        myevent-appl_event = 'X'.
        myevent-eventid = go_arr_cal->m_id_pre_selection.
        APPEND myevent TO myevent_tab.

        myevent-eventid = go_arr_cal->m_id_info_request.
        APPEND myevent TO myevent_tab.

        myevent-eventid = go_arr_cal->m_id_ctxmenu_request.
        APPEND myevent TO myevent_tab.

        myevent-eventid = go_arr_cal->m_id_ctxmenu_selected.
        APPEND myevent TO myevent_tab.

        CALL METHOD go_arr_cal->set_registered_events
          EXPORTING
            events = myevent_tab.

      ENDIF.

  ENDCASE.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_table
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_table USING po_object
                   TYPE REF TO cl_gui_alv_grid.

*-- ALV 초기화
  DATA : ls_stable TYPE lvc_s_stbl.

  ls_stable = VALUE #(
                        row = abap_true
                        col = abap_true
                     ).
  po_object->refresh_table_display(
    EXPORTING
      is_stable = ls_stable ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_rowh
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_rowh .

*-- 정비 오더 헤더 삭제
  DATA : lt_row_id TYPE lvc_t_roid,
         ls_row_id TYPE lvc_s_roid.

*-- 삭제 행 읽어오기
  CALL METHOD go_disp_ooh_alv->get_selected_rows
    IMPORTING
      et_row_no = lt_row_id.

*-- 삭제 행 없으면 오류 메시지
  IF lt_row_id IS INITIAL.
    MESSAGE s010 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

*-- 정비 오더 헤더 초기화
  CLEAR : gt_del_ooh.

  SORT lt_row_id BY row_id DESCENDING.

  LOOP AT lt_row_id INTO ls_row_id.

    CLEAR : gs_disp_ooh, gs_del_ooh.

    READ TABLE gt_disp_ooh INTO gs_disp_ooh INDEX ls_row_id-row_id.

    MOVE-CORRESPONDING gs_disp_ooh TO gs_del_ooh.

    APPEND gs_del_ooh TO gt_del_ooh.

    DELETE gt_disp_ooh INDEX ls_row_id-row_id.

*-- 정비 오더 헤더에 해당하는 아이템 화면에서 삭제
    LOOP AT gt_disp_ooi ASSIGNING FIELD-SYMBOL(<fs>) WHERE order_id = gs_del_ooh-order_id.

      CLEAR gs_del_ooi.

      gs_del_ooi = CORRESPONDING #( <fs> ).

      APPEND gs_del_ooi to gt_del_ooi.

      DELETE gt_disp_ooi WHERE operation_id = <fs>-operation_id.

    ENDLOOP.

  ENDLOOP.

  PERFORM refresh_table USING go_disp_ooh_alv.

  PERFORM refresh_table USING go_disp_ooi_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form delete_rowi
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM delete_rowi .

*-- 정비 오더 아이템 삭제
  DATA : lt_row_id TYPE lvc_t_roid,
         ls_row_id TYPE lvc_s_roid.

*-- 삭제 행 읽어오기
  CALL METHOD go_disp_ooi_alv->get_selected_rows
    IMPORTING
      et_row_no = lt_row_id.

*-- 삭제 행 없으면 오류 메시지
  IF lt_row_id IS INITIAL.
    MESSAGE s010 DISPLAY LIKE 'E'.
    EXIT.
  ENDIF.

*-- 정비 오더 아이템 초기화
  CLEAR : gt_del_ooi.

  SORT lt_row_id BY row_id DESCENDING.

  LOOP AT lt_row_id INTO ls_row_id.

    CLEAR : gs_disp_ooi, gs_del_ooi.

    READ TABLE gt_disp_ooi INTO gs_disp_ooi INDEX ls_row_id-row_id.

    MOVE-CORRESPONDING gs_disp_ooi TO gs_del_ooi.

    APPEND gs_del_ooi TO gt_del_ooi.

    DELETE gt_disp_ooi INDEX ls_row_id-row_id.

  ENDLOOP.

  PERFORM refresh_table USING go_disp_ooi_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form salv_display
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM salv_display USING pv_aircraft_id TYPE zc103e_pm_acft_id.

*-- SALV 출력 위한 변수 설정
  DATA: lo_container TYPE REF TO cl_gui_dialogbox_container,
        lo_columns   TYPE REF TO cl_salv_columns_table,
        lo_column    TYPE REF TO cl_salv_column_table,
        ls_color     TYPE lvc_s_colo.

*-- 선택한 행에 대한 데이터 조회
  CLEAR gt_air.

  SELECT
    FROM zc103pmt0001
    FIELDS aircraft_id, aircraft_type, in_service_date, status, remarks
    WHERE aircraft_id = @pv_aircraft_id
    INTO CORRESPONDING FIELDS OF TABLE @gt_air.

*-- 팝업 컨테이너 생성
  CREATE OBJECT lo_container
    EXPORTING
      width   = 600
      height  = 100
      top     = 100
      left    = 100
      caption = '항공기 정보'.

*-- 핸들러 연결 (X버튼 눌렀을 때 close 이벤트 처리)
  SET HANDLER lcl_event_handler=>on_close FOR lo_container.

*-- Simple ALV 출력
  TRY.

*-- 리스트 생성
      cl_salv_table=>factory(

  EXPORTING
    r_container  = lo_container
        IMPORTING
          r_salv_table = DATA(lo_alv)
        CHANGING
          t_table      = gt_air  ).

*-- 열 너비 자동 최적화
      lo_alv->get_columns( )->set_optimize( ).

*-- 키 부분을 키처럼 색칠
      lo_columns = lo_alv->get_columns( ).

      TRY.

          lo_column ?= lo_columns->get_column( 'AIRCRAFT_ID' ).

        CATCH cx_salv_not_found INTO DATA(lx_salv_nf).

          MESSAGE lx_salv_nf->get_text( ) TYPE 'E'.

      ENDTRY.

      ls_color-col = 1.

      ls_color-int = 1.

      lo_column->set_color( ls_color ).

*-- 색칠 후 출력
      lo_alv->display( ).

    CATCH cx_salv_msg INTO DATA(lx_salv).

  ENDTRY.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_date
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_date .

*-- 기준 날짜 설정( sy-datum 들어갈 예정 )
  gv_dep_date = sy-datum.

*-- 기준 날짜로부터 한달 뒤 날짜 설정
  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      date      = gv_dep_date
      days      = 0
      months    = 1
      years     = 0
    IMPORTING
      calc_date = gv_arr_date.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM get_data .

  CLEAR gt_ooh.

*-- 정비 오더 헤더 원본 데이터 AMDP로 조회
  zclc103pm_amdp01=>get_ooh(
    IMPORTING
      et_result     = gt_ooh
  ).

*-- 정렬
  SORT gt_ooh BY order_id.

*-- 정비 오더 헤더 데이터 세팅
  PERFORM set_data_0103.

*-- 정비 오더 아이템 원본 데이터 인터널 테이블 대상으로 조회
  SELECT
    FROM zc103pmt0005 AS a
    INNER JOIN @gt_disp_ooh AS b
    ON a~order_id = b~order_id
    FIELDS a~order_id, a~operation_id, a~description, a~duration, a~skill_type, a~remarks,
           a~erdat, a~erzet, a~ernam, a~aedat, a~aezet, a~aenam
    INTO CORRESPONDING FIELDS OF TABLE @gt_ooi.

  SORT gt_ooi BY order_id operation_id.

*-- 정비 오더 아이템 데이터 세팅
  gt_disp_ooi = CORRESPONDING #( gt_ooi ).
  IF lines( gt_disp_ooh ) IS INITIAL.

    MESSAGE s003 DISPLAY LIKE 'E' .

  ELSE.

    MESSAGE |정비 오더 { lines( gt_disp_ooh ) }건, 작업 항목 { lines( gt_disp_ooi ) }건이 검색되었습니다. | TYPE 'S'.

  ENDIF.

*-- 도메인 테이블 조회
  SELECT
    FROM dd07v
    FIELDS domvalue_l, ddtext
    WHERE domname = 'ZC103D_FI_LAND'
    INTO CORRESPONDING FIELDS OF TABLE @gt_dd07v.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_data_0103
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_data_0103 .

  CLEAR : gt_disp_ooh, gv_d, gv_m, gv_r, gv_o.

*-- 조회용 데이터 세팅
  LOOP AT gt_ooh ASSIGNING FIELD-SYMBOL(<fs>) WHERE plan_date BETWEEN gv_dep_date AND gv_arr_date.

    gs_disp_ooh = CORRESPONDING #( <fs> ).

*-- 정비 유형별 개수 세팅
    CASE <fs>-maint_type.
      WHEN 'D'.
        gv_d += 1.
      WHEN 'M'.
        gv_m += 1.
      WHEN 'R'.
        gv_r += 1.
      WHEN 'O'.
        gv_o += 1.
    ENDCASE.

*-- APPEND와 같은 역할
    gt_disp_ooh = VALUE #( BASE gt_disp_ooh ( gs_disp_ooh ) ).

  ENDLOOP.

  PERFORM set_screen.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_screen .

  DATA : ls_styl  TYPE lvc_s_styl,
         lv_tabix TYPE sy-tabix.

  CLEAR : gs_disp_ooh,gs_disp_ooi.

*-- 화면 설정
  LOOP AT gt_disp_ooh INTO gs_disp_ooh.

    lv_tabix = sy-tabix.

    CLEAR : ls_styl, gs_disp_ooh-cell_tab.
    PERFORM set_enabled USING ' ' 'H'
                        CHANGING ls_styl.
    PERFORM set_disabled USING 'ORDER_ID' 'H'
                         CHANGING ls_styl.
    PERFORM set_disabled USING 'AIRCRAFT_ID' 'H'
                         CHANGING ls_styl.
    PERFORM set_disabled USING 'SCHEDULEID' 'H'
                         CHANGING ls_styl.
    PERFORM set_disabled USING 'MAINT_TYPE' 'H'
                         CHANGING ls_styl.
    PERFORM set_disabled USING 'PLAN_DATE' 'H'
                         CHANGING ls_styl.
    MODIFY gt_disp_ooh FROM gs_disp_ooh INDEX lv_tabix TRANSPORTING cell_tab.

  ENDLOOP.

  LOOP AT gt_disp_ooi INTO gs_disp_ooi.

    lv_tabix = sy-tabix.

    CLEAR : ls_styl, gs_disp_ooi-cell_tab.
    PERFORM set_enabled USING ' ' 'I'
                        CHANGING ls_styl.
    PERFORM set_disabled USING 'ORDER_ID' 'I'
                         CHANGING ls_styl.
    PERFORM set_disabled USING 'OPERATION_ID' 'I'
                         CHANGING ls_styl.
    MODIFY gt_disp_ooi FROM gs_disp_ooi INDEX lv_tabix TRANSPORTING cell_tab.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form toggle_changeh
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
FORM toggle_changeh.

*-- 조회 <-> 수정 모드
  CASE gv_modeh.
    WHEN 'E'.
      go_disp_ooh_alv->set_ready_for_input( 0 ).
      gv_modeh = 'R'.
    WHEN 'R'.
      go_disp_ooh_alv->set_ready_for_input( 1 ).
      gv_modeh = 'E'.
  ENDCASE.
  PERFORM set_screen.
  PERFORM refresh_table USING go_disp_ooh_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form toggle_changei
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
FORM toggle_changei.

*-- 조회 <-> 수정 모드
  CASE gv_modei.
    WHEN 'E'.
      go_disp_ooi_alv->set_ready_for_input( 0 ).
      gv_modei = 'R'.
    WHEN 'R'.
      go_disp_ooi_alv->set_ready_for_input( 1 ).
      gv_modei = 'E'.
  ENDCASE.
  PERFORM set_screen.
  PERFORM refresh_table USING go_disp_ooi_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_disabled
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&      <-- LS_STYL
*&---------------------------------------------------------------------*
FORM set_disabled USING pv_field pv_itab
                  CHANGING ps_styl TYPE lvc_s_styl.

  ps_styl-fieldname = pv_field.
  ps_styl-style     = cl_gui_alv_grid=>mc_style_disabled.
  CASE pv_itab.
    WHEN 'H'.
      INSERT ps_styl INTO TABLE gs_disp_ooh-cell_tab.
    WHEN 'I'.
      INSERT ps_styl INTO TABLE gs_disp_ooi-cell_tab.
  ENDCASE.
  CLEAR ps_styl.
ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_enabled
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&---------------------------------------------------------------------*
FORM set_enabled USING pv_field pv_itab
                 CHANGING ps_styl TYPE lvc_s_styl .

  ps_styl-fieldname = pv_field.
  ps_styl-style     = cl_gui_alv_grid=>mc_style_enabled.
  CASE pv_itab.
    WHEN 'H'.
      APPEND ps_styl TO gs_disp_ooh-cell_tab.
    WHEN 'I'.
      APPEND ps_styl TO gs_disp_ooi-cell_tab.
  ENDCASE.
  CLEAR ps_styl.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_data
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_data .

*-- 변경 사항 저장
  DATA : lv_answer,
         lt_saveh   TYPE TABLE OF zc103pmt0004,
         ls_saveh   TYPE zc103pmt0004,
         lt_savei   TYPE TABLE OF zc103pmt0005,
         ls_savei   TYPE zc103pmt0005,
         lt_deli    TYPE TABLE OF zc103pmt0005,
         lv_check   TYPE abap_bool,
         lt_changed TYPE lvc_s_modi,
         lv_subrch  TYPE sy-subcs,
         lv_subrci  TYPE sy-subcs.

  CALL METHOD go_disp_ooh_alv->check_changed_data.

  CALL METHOD go_disp_ooi_alv->check_changed_data.

  CLEAR : gs_disp_ooh, gs_disp_ooi.

  READ TABLE gt_disp_ooh INTO gs_disp_ooh WITH KEY modi_yn = abap_true.

  lv_subrch = sy-subrc.

  READ TABLE gt_disp_ooi INTO gs_disp_ooi WITH KEY modi_yn = abap_true.

  lv_subrci = sy-subrc.

  IF ( lv_subrch <> 0 ) AND ( lv_subrci <> 0 ) AND
     ( gt_del_ooh IS INITIAL AND gt_del_ooi IS INITIAL ).

    MESSAGE s000 WITH TEXT-i01 DISPLAY LIKE 'E'.

    EXIT.

  ENDIF.

*-- 팝업창
  PERFORM get_popup_to_confirm USING '저장'
                               CHANGING lv_answer.

  CHECK lv_answer = '1'.

  PERFORM set_date.

  MOVE-CORRESPONDING gt_disp_ooh TO lt_saveh.
  MOVE-CORRESPONDING gt_disp_ooi TO lt_savei.

*-- 정비 오더 헤더 데이터 삭제
  IF gt_del_ooh IS NOT INITIAL.

    DELETE zc103pmt0004 FROM TABLE gt_del_ooh.
    CLEAR gt_del_ooh.

  ENDIF.

*-- 정비 오더 아이템 데이터 삭제
  IF gt_del_ooi IS NOT INITIAL.

    DELETE zc103pmt0005 FROM TABLE gt_del_ooi.
    CLEAR gt_del_ooi.

  ENDIF.

*-- 수정 및 생성 데이터 반영
  MODIFY zc103pmt0004 FROM TABLE lt_saveh.
  MODIFY zc103pmt0005 FROM TABLE lt_savei.

  IF sy-subrc = 0.
    MESSAGE s000 WITH '성공하였습니다'.
    COMMIT WORK.
    PERFORM get_data.
    PERFORM set_data_0103.
    PERFORM refresh_table USING go_disp_ooh_alv.
    PERFORM refresh_table USING go_disp_ooi_alv.
  ELSE.
    MESSAGE s000 WITH 'Error' DISPLAY LIKE 'E'.
    ROLLBACK WORK.
  ENDIF.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_date
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- lt_saveh
*&---------------------------------------------------------------------*
FORM set_date.

*-- 날짜 설정
  LOOP AT gt_disp_ooh ASSIGNING FIELD-SYMBOL(<fs>) WHERE modi_yn = abap_true.

    IF gs_disp_ooh-erdat IS INITIAL.
      <fs> = VALUE #( BASE <fs> erdat = sy-datum
                                ernam = sy-uname
                                erzet = sy-uzeit ).
    ELSE.
      <fs> = VALUE #( BASE <fs> aedat = sy-datum
                                aenam = sy-uname
                                aezet = sy-uzeit ).
    ENDIF.

    <fs>-modi_yn = abap_false.

  ENDLOOP.

  LOOP AT gt_disp_ooi ASSIGNING FIELD-SYMBOL(<fs2>) WHERE modi_yn = abap_true.

    IF gs_disp_ooi-erdat IS INITIAL.
      <fs2> = VALUE #( BASE <fs2> erdat = sy-datum
                                ernam = sy-uname
                                erzet = sy-uzeit ).

    ELSE.
      <fs2> = VALUE #( BASE <fs2> aedat = sy-datum
                                aenam = sy-uname
                                aezet = sy-uzeit ).
    ENDIF.

    <fs2>-modi_yn = abap_false.

  ENDLOOP.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form get_popup_to_confirm
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- LV_ANSWER
*&---------------------------------------------------------------------*
FORM get_popup_to_confirm USING pv_title
                          CHANGING pv_answer.

  call function 'ZC1F030001'
    EXPORTING
      iv_action = pv_title
    IMPORTING
      ev_answer = pv_answer.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object_0103
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object_0103 .

*-- 배경 컨테이너 생성
  _dock go_base_con.

*-- 스플리터 컨테이너 생성
  _split_create go_base_spt go_base_con 1 2.

*-- 정비 오더 헤더 인스턴스 생성
  _split_get go_base_spt 1 1 go_disp_ooh_con.
  _obj go_disp_ooh_alv go_disp_ooh_con.

*-- 정비 오더 아이템 인스턴스 생성
  _split_get go_base_spt 1 2 go_disp_ooi_con.
  _obj go_disp_ooi_alv go_disp_ooi_con.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form exclude_toolbar
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM exclude_toolbar .

*-- 툴바 제외
  DATA : ls_ui_functions TYPE ui_func.

  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_undo.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_copy_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_cut.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_delete_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_insert_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_append_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_loc_paste_new_row.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_refresh.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_auf.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_average.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_print.
  APPEND ls_ui_functions TO gt_ui_functions.
  ls_ui_functions = cl_gui_alv_grid=>mc_fc_graph.
  APPEND ls_ui_functions TO gt_ui_functions.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_field_catalog_0103
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_field_catalog_0103 .

*-- 정비 오더 헤더 fcat 설정
  gt_disp_ooh_fcat = VALUE #(
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'ORDER_ID'    key = abap_true )
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'AIRCRAFT_ID' hotspot = abap_true )
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'SCHEDULEID' )
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'MAINT_TYPE' )
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'PLAN_DATE' )
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'RELEASE_DATE' )
                              ( ref_table = 'ZC103PMT0004' just = 'C' fieldname = 'CLOSE_DATE' )
                              ( ref_table = 'ZC103PMT0004'            fieldname = 'REMARKS'     emphasize = abap_true )
                           ).

*-- 정비 오더 아이템 fcat 설정ㅁ
  gt_disp_ooi_fcat = VALUE #(
                              ( ref_table = 'ZC103PMT0005' fieldname = 'ORDER_ID'     key = abap_true  )
                              ( ref_table = 'ZC103PMT0005' fieldname = 'OPERATION_ID' key = abap_true )
                              ( ref_table = 'ZC103PMT0005' fieldname = 'DESCRIPTION'  emphasize = abap_true )
                              ( ref_table = 'ZC103PMT0005' fieldname = 'DURATION' )
                              ( ref_table = 'ZC103PMT0005' fieldname = 'SKILL_TYPE' )
                              ( ref_table = 'ZC103PMT0005' fieldname = 'REMARKS'      emphasize = abap_true )
                           ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_layout
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_layout .

*-- 레이아웃 설정
  gs_disp_ooh_layout  = VALUE #( zebra = abap_true
                                  stylefname = 'CELL_TAB'
                                 cwidth_opt = 'A'
                                 sel_mode = 'D'
                                 grid_title = '정비 오더'
                               ).

  gs_disp_ooi_layout  = VALUE #( zebra = abap_true
                                  stylefname = 'CELL_TAB'
                                 cwidth_opt = 'A'
                                 sel_mode = 'D'
                                 grid_title = '작업 오더'
                               ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_event_0103
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_event_0103.

*-- 이벤트 설치
  SET HANDLER : lcl_event_handler=>edit_toolbar  FOR go_disp_ooh_alv,
                lcl_event_handler=>user_command  FOR go_disp_ooh_alv,
                lcl_event_handler=>double_click  FOR go_disp_ooh_alv,
                lcl_event_handler=>hotspot_click FOR go_disp_ooh_alv,
                lcl_event_handler=>modify_value  FOR go_disp_ooh_alv,
                lcl_event_handler=>edit_toolbar  FOR go_disp_ooi_alv,
                lcl_event_handler=>modify_value  FOR go_disp_ooi_alv,
                lcl_event_handler=>user_command  FOR go_disp_ooi_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form display_alv_0103
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM display_alv_0103 .

*-- Set variant handle & Dispaly alv
  gs_variant-handle = 'OOH'.
  go_disp_ooh_alv->set_table_for_first_display(
    EXPORTING
      is_variant                    = gs_variant
      i_save                        = 'A'
      i_default                     = 'X'
      is_layout                     = gs_disp_ooh_layout
      it_toolbar_excluding          = gt_ui_functions
    CHANGING
      it_outtab                     = gt_disp_ooh
      it_fieldcatalog               = gt_disp_ooh_fcat
  ).

  gs_variant-handle = 'OOI'.
  go_disp_ooi_alv->set_table_for_first_display(
    EXPORTING
      is_variant                    = gs_variant
      i_save                        = 'A'
      i_default                     = 'X'
      is_layout                     = gs_disp_ooi_layout
      it_toolbar_excluding          = gt_ui_functions
    CHANGING
      it_outtab                     = gt_disp_ooi
      it_fieldcatalog               = gt_disp_ooi_fcat
  ).

  PERFORM register_event_0103.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event_0103
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event_0103 .

*--  Ready For Input
  CASE gv_modeh.
    WHEN 'E'.
      go_disp_ooh_alv->set_ready_for_input( 1 ).
    WHEN 'R'.
      go_disp_ooh_alv->set_ready_for_input( 0 ).
  ENDCASE.

  CASE gv_modei.
    WHEN 'E'.
      go_disp_ooi_alv->set_ready_for_input( 1 ).
    WHEN 'R'.
      go_disp_ooi_alv->set_ready_for_input( 0 ).
  ENDCASE.

*-- Edit Event
  go_disp_ooh_alv->register_edit_event( cl_gui_alv_grid=>mc_evt_enter ).
  go_disp_ooi_alv->register_edit_event( cl_gui_alv_grid=>mc_evt_enter ).

ENDFORM.
*&---------------------------------------------------------------------*
*& Form create_object_0101
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM create_object_0101 .

*-- 켈린더 오브젝트 생성 화면
  DATA : r3_today TYPE cnca_utc_date.

*-- From 인스턴스 생성
  CREATE OBJECT go_dep_con
    EXPORTING
      container_name = 'C_PICKER_DEP'.

*-- To 인스턴스 생성
  CREATE OBJECT go_arr_con
    EXPORTING
      container_name = 'C_PICKER_ARR'.

*-- 달력 모양 + 날짜 선택 화면
  gv_calendar_style = cnca_style_v_navigator +
                       cnca_style_dtpicker.

*-- 하루씩 선택
  gv_selection_style = cnca_sel_day.
*-- 여러개 선택할 수 있는 옵션
*  + cnca_sel_week +  cnca_sel_month + cnca_sel_interval.

*-- 설정한 날짜를 focus date로 설정
  r3_today = gv_dep_date.
  CREATE OBJECT go_dep_cal
    EXPORTING
      parent          = go_dep_con
      view_style      = gv_calendar_style
      selection_style = gv_selection_style
      focus_date      = r3_today.

  r3_today = gv_arr_date.
  CREATE OBJECT go_arr_cal
    EXPORTING
      parent          = go_arr_con
      view_style      = gv_calendar_style
      selection_style = gv_selection_style
      focus_date      = r3_today.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form set_event_0101
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM set_event_0101 .

*-- 켈린더에 이벤트 설정
  SET HANDLER : lcl_event_handler=>on_date_selected             FOR go_dep_cal,
                lcl_event_handler=>on_date_selected             FOR go_arr_cal.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form register_event_0101
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM register_event_0101 .

*-- 켈린더 인스턴스에 이벤트 설정( 잘 모름 )
*   handling as system event
  myevent-appl_event = ' '.
  myevent-eventid = go_dep_cal->m_id_date_selected.
  APPEND myevent TO myevent_tab.

*   handling as application event
  myevent-appl_event = 'X'.
  myevent-eventid = go_dep_cal->m_id_pre_selection.
  APPEND myevent TO myevent_tab.

  myevent-eventid = go_dep_cal->m_id_info_request.
  APPEND myevent TO myevent_tab.

  myevent-eventid = go_dep_cal->m_id_ctxmenu_request.
  APPEND myevent TO myevent_tab.

  myevent-eventid = go_dep_cal->m_id_ctxmenu_selected.
  APPEND myevent TO myevent_tab.

  CALL METHOD go_dep_cal->set_registered_events
    EXPORTING
      events = myevent_tab.
*   handling as system event
  myevent-appl_event = ' '.
  myevent-eventid = go_arr_cal->m_id_date_selected.
  APPEND myevent TO myevent_tab.

*   handling as application event
  myevent-appl_event = 'X'.
  myevent-eventid = go_arr_cal->m_id_pre_selection.
  APPEND myevent TO myevent_tab.

  myevent-eventid = go_arr_cal->m_id_info_request.
  APPEND myevent TO myevent_tab.

  myevent-eventid = go_arr_cal->m_id_ctxmenu_request.
  APPEND myevent TO myevent_tab.

  myevent-eventid = go_arr_cal->m_id_ctxmenu_selected.
  APPEND myevent TO myevent_tab.

  CALL METHOD go_arr_cal->set_registered_events
    EXPORTING
      events = myevent_tab.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_header
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_header .

  MODIFY zc103pmt0004 FROM gs_save_ooh.

  MESSAGE '정비 오더 헤더 생성 완료' TYPE 'S'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form save_item
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM save_item .

  MODIFY zc103pmt0005 FROM gs_save_ooi.

  MESSAGE '정비 오더 아이템 생성 완료' TYPE 'S'.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form search_param
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> P_
*&---------------------------------------------------------------------*
FORM search_param  USING pa_id LIKE gs_ooh-aircraft_id.

  LOOP AT gt_ooh ASSIGNING FIELD-SYMBOL(<fs>) WHERE aircraft_id = pa_id AND plan_date BETWEEN gv_dep_date AND gv_arr_date.

    CLEAR gs_disp_ooh.

    gs_disp_ooh = CORRESPONDING #( <fs> ).

    APPEND gs_disp_ooh TO gt_disp_ooh.

*-- Count Check Number
    CASE <fs>-maint_type.
      WHEN 'D'.
        gv_d += 1.
      WHEN 'M'.
        gv_m += 1.
      WHEN 'R'.
        gv_r += 1.
      WHEN 'O'.
        gv_o += 1.
    ENDCASE.

  ENDLOOP.

  PERFORM refresh_table USING go_disp_ooh_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form search_ooi
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM search_ooi .

  CLEAR gt_disp_ooi.

  SELECT
    FROM zc103pmt0005 AS a
    INNER JOIN @gt_disp_ooh AS b
    ON a~order_id = b~order_id
    FIELDS a~order_id, a~operation_id, a~description, a~duration, a~skill_type, a~remarks,
           a~erdat, a~erzet, a~ernam, a~aedat, a~aezet, a~aenam
    INTO CORRESPONDING FIELDS OF TABLE @gt_disp_ooi.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form refresh_screen
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM refresh_screen .
  p01 = 'X'.
  p02 = 'X'.
  p03 = 'X'.
  p04 = 'X'.
  p05 = 'X'.
  p06 = 'X'.
  PERFORM get_date.
  PERFORM get_data.
  go_disp_ooh_alv->set_ready_for_input( 0 ).
  go_disp_ooi_alv->set_ready_for_input( 0 ).
  gv_modeh = 'R'.
  gv_modei = 'R'.
  go_arr_cal->free( ).
  go_arr_con->free( ).
  go_dep_cal->free( ).
  go_dep_con->free( ).
  FREE : go_arr_cal, go_arr_con, go_dep_cal, go_dep_con.
  CLEAR : gs_sch.
  PERFORM create_object_0101.
  PERFORM set_event_0101.
  PERFORM register_event_0101.
  PERFORM set_screen.
  PERFORM refresh_table USING go_disp_ooh_alv.
  PERFORM refresh_table USING go_disp_ooi_alv.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form search_msg
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM search_msg .

  SORT gt_disp_ooh BY order_id.

  SORT gt_disp_ooi BY order_id operation_id.

  IF lines( gt_disp_ooh ) IS INITIAL.

    MESSAGE s003 DISPLAY LIKE 'E' .

  ELSE.

    MESSAGE |정비 오더 { lines( gt_disp_ooh ) }건, 작업 항목 { lines( gt_disp_ooi ) }건이 검색되었습니다. | TYPE 'S'.

  ENDIF.


ENDFORM.
